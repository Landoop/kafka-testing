buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.maiflai:gradle-scalatest:0.16'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
        classpath 'net.researchgate:gradle-release:2.4.0'
    }
}

apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'net.researchgate.release'


allprojects {
    group = 'com.landoop'
    version = version
    description = "Kafka Unit Testing: Embeded broker, ZK, SR, Connect services"

    apply plugin: 'scala'
    apply plugin: 'maven'
    apply plugin: 'com.github.maiflai.scalatest'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        scalaMajorVersion = '2.12'
        scala = '2.12.3'
        scalaCheck = '1.13.5'
        kafkaVersion = "0.10.2.1"
        confluentVersion = "3.2.2"
        scalaTest = '3.0.3'
        junitVersion = '4.12'
        mockitoVersion = '2.8.47'
        avroVersion = '1.8.1'
        avro4sVersion = "1.6.2"
        scalaLoggingVersion = '3.7.2'
        bouncycastleVersion = "1.57"
        apacheDirectoryVersion = "2.0.0-M24"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.typesafe.com/typesafe/releases/" }
        maven { url "http://packages.confluent.io/maven/" }
    }

    configurations {
        provided
        compile.extendsFrom provided
    }

    dependencies {

        compile "org.scala-lang:scala-library:$scala"
        compile "com.typesafe.scala-logging:scala-logging_$scalaMajorVersion:$scalaLoggingVersion"
        compile("io.confluent:kafka-schema-registry:$confluentVersion")

        compile("io.confluent:kafka-schema-registry-client:$confluentVersion") {
            exclude group: "com.fasterxml.jackson.core"
            exclude group: "jackson-databind"
        }
        compile("org.apache.kafka:kafka-clients:$kafkaVersion")
        compile("io.confluent:kafka-avro-serializer:$confluentVersion")
        compile("org.apache.avro:avro:$avroVersion")

        compile("org.apache.kafka:kafka_$scalaMajorVersion:$kafkaVersion")
        compile("org.apache.kafka:kafka_$scalaMajorVersion:$kafkaVersion:test@jar")
        compile("org.apache.kafka:kafka-clients:$kafkaVersion:test@jar")
        compile("io.confluent:common-config:$confluentVersion")
        compile("io.confluent:common-utils:$confluentVersion")
        compile("io.confluent:rest-utils:$confluentVersion")
        compile("org.apache.kafka:connect-runtime:$kafkaVersion")

        compile("junit:junit:$junitVersion")
        compile("org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion")
        compile("org.apache.directory.server:apacheds-core-api:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-interceptor-kerberos:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-protocol-shared:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-protocol-kerberos:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-protocol-ldap:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-ldif-partition:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-mavibot-partition:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-jdbm-partition:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-all:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.shared", module: "shared-ldap-schema"
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-server-integ:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.shared", module: "shared-ldap-schema"
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }
        compile("org.apache.directory.server:apacheds-core-integ:$apacheDirectoryVersion") {
            exclude group: "org.apache.directory.shared", module: "shared-ldap-schema"
            exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
        }


        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.scalacheck:scalacheck_$scalaMajorVersion:$scalaCheck"
        testCompile "org.scalatest:scalatest_$scalaMajorVersion:$scalaTest"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.apache.curator:curator-test:3.1.0"
        testCompile 'org.powermock:powermock-module-junit4:1.6.5'
        testCompile 'org.pegdown:pegdown:1.1.0'
    }

    test {
        maxParallelForks = 1
        minHeapSize '256m'
        maxHeapSize '2048m'
        systemProperty 'keystore', projectDir.canonicalPath + "/src/test/resources/stc_keystore.jks"
        systemProperty 'truststore', projectDir.canonicalPath + "/src/test/resources/stc_truststore.jks"
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task scaladocJar(type: Jar) {
        classifier = 'scaladoc'
        from '../LICENSE'
        from scaladoc
    }

    tasks.withType(Tar) {
        compression Compression.GZIP
        extension = 'tgz'
    }

    artifacts {
        archives javadocJar, scaladocJar, sourcesJar
    }

    task compile(dependsOn: 'compileScala')

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    // OSSRH publication
    if (project.hasProperty('release')) {
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/landoop/kafka-testing'

                        scm {
                            connection 'scm:git:https://github.com/landoop/kafka-testing.git'
                            developerConnection 'scm:git:git@github.com:landoop/kafka-testing.git'
                            url 'https://github.com/landoop/kafka-testing.git'
                        }

                        licenses {
                            license {
                                name 'Apache License 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'stheppi'
                                name = 'Stefan Bocutiu'
                                email = 'stefan@landoop.com'
                            }
                            developer {
                                id = 'Antwnis'
                                name = 'Antonios Chalkiopoulos'
                                email = 'antonios@landoop.com'
                            }
                        }
                    }
                }
            }
        }

        nexusStaging {
            packageGroup = project.getGroup()
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

